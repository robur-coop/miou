{1 Sleepers: how to inject syscalls into [miou]?}

This tutorial shows how to inject a new syscall to [miou] and extend the API of it
with {i blocking} operations. For the example, we're going to implement the
sleepers. [Unix.sleepf] is a blocking operation. The fundamental problem with
[miou] is that it performs operations in the background (scheduling). So using a
blocking operation with [miou] prevents it from managing other tasks
concurrently (manage tasks entered with {!val:Miou.call_cc}) or in parallel
(wait for parallel process tasks introduced by {!val:Miou.call}). 

As stated in the documentation, and this is a fundamental rule:
> you should never give [miou] blocking tasks (such as [Unix.sleepf])

That said, how do you manage blocking tasks? [miou] offers an API that allows
you to extend its API with such tasks. The idea is to inform [miou] of a promise
that is {b not} associated with a task (because the latter would be blocking)
and to notify it of the task that we would like to do as soon as we are {b sure}
that it is a non-blocking task.

This notification is possible because [miou] lets you inject such a function
which will notify it. This is the [events] function expected as a parameter to
the {!val:Miou.run} function.

{2 What we want to do?}

So let's get down to business. The aim of this tutorial is to enable you to
write this code:

{[
open Miou

let program () =
  Miou.run @@ fun () ->
  let a = Miou.call_cc (fun () -> sleep 1.) in
  let b = Miou.call_cc (fun () -> sleep 2.) in
  Miou.await_all [ a; b ]
  |> List.iter @@ function Ok () -> () | Error exn -> raise exn

let () =
  let t0 = Unix.gettimeofday () in
  program ();
  let t1 = Unix.gettimeofday () in
  assert (t1 -. t0 < 3.)
]}

This code explains simple behaviour: our tasks [a] and [b] should run
concurrently. In other words, in the end, we should consume strictly less than 3
seconds (about 2 seconds) to complete this little program.

You can have fun replacing [sleep] with [Unix.sleepf] and you'll see that we're
back to a simple sequential execution where we need 3 seconds to finish the
program. And that's normal, [miou] doesn't know that [Unix.sleepf] is blocking,
so it will execute the two tasks one after the other without scheduling them
correctly.

So we've got our test, which will validate what we're expecting.

{2 Syscalls.}

The {!module:Miou} module is fairly straightforward about what can (and
cannot) be done about {i promises} not associated with a task. You can create
such a promise ({!val:Miou.make}), {b suspend} and wait for its result
{!val:Miou.suspend}) and obtain its unique identifier ({!val:Miou.uid}).
And that's it!

The first function allows us to create our [sleep] "syscall". The second will
allow us to specify the point at which we would like to obtain the result of our
blocking operation and the third function will allow us to {b keep} (and
{b store}) this {i promise} so that we can find it again later.

{[
open Miou

let sleepers = Hashtbl.create 0x100

let sleep until =
  let return () = () in
  let promise = Miou.make return in
  Hashtbl.add sleepers (Miou.uid promise) (promise, until);
  match Miou.suspend promise with Ok () -> () | Error exn -> raise exn
]}

As you can see, the implementation of a 'syscall' is relatively simple, but it
is always associated with the implementation or extension of another function:
the [events] function. The [return] value is the function that is called as
soon as the promise {b is} resolved. In our case, we would like to return
[() : unit] but we could very well return the value of a reference that the task
of resolving our promise will modify correctly.

[miou] is quite stupid, trying to carry out all the tasks we give it in the hope
that they will solve our promises. And it does this as long as it has at least
one unresolved promise. In our case, the promise we've just created will never
be resolved by any task. To clarify [miou]'s behaviour in this situation, you
can run this code:

{[
let dummy _ =
  { select= Fun.const []
  ; interrupt= ignore }

let () = Miou.(run ~events:dummy @@ fun () -> sleep 1.; ())
]}

This code will never end simply because a promise is always waiting to be
solved.

But as you can see, I've specified an [events] function here which always
returns an empty list. In truth, if [miou] has no more tasks to do and there are
still promises, it will try one last thing: execute our [events] function. This
can return a new task (always non-blocking) that could resolve a given promise.
And it's here that we'll be able to inject the tasks that will resolve our
sleepers.

Contrary to what we have just said, this [events] function (and only this one)
{b can block}! And, in reality, this is not a problem as all the tasks have been
executed. We can therefore be in a {i busy waiting} state for the next event to
unblock our execution flow.

In our case, it's a case of taking the {b smallest} sleeper, waiting and then
returning a task that resolves that same sleeper. We also need to update the
other sleepers because we're going to consume time.

{[
let select () =
  let min =
    Hashtbl.fold
      (fun uid (prm, until) -> function
        | Some (_uid', _prm', until') when until < until' ->
            Some (uid, prm, until)
        | Some _ as acc -> acc
        | None -> Some (uid, prm, until))
      sleepers None
  in
  match min with
  | None -> []
  | Some (uid, prm, until) ->
      Hashtbl.remove sleepers uid;
      Hashtbl.filter_map_inplace
        (fun _ (prm, until') -> Some (prm, Float.max 0. (until' -. until)))
        sleepers;
      Unix.sleepf until;
      [ Miou.task prm (Fun.const ()) ]

let events _ = { select; interrupt= ignore }
]}

{2 Usage.}

Now that we have our [events] function and our {i syscall} [sleep], we can use
them:

{[
let prgm () =
  Miou.run ~events @@ fun () ->
  let a = Miou.call_cc (fun () -> sleep 1.) in
  let b = Miou.call_cc (fun () -> sleep 2.) in
  ignore (Miou.await a);
  ignore (Miou.await b)

let () =
  let t0 = Unix.gettimeofday () in
  prgm ();
  let t1 = Unix.gettimeofday () in
  assert (t1 -. t0 < 3.)
]}

Note that our [events] function has been transferred to {!val:Miou.run}! Without
it, our code wouldn't work. And that's it! Our program did not fail to run,
which means that we used {b less} than 3 seconds (about 2).

{[
$ ocamlfind opt -linkpkg -package miou main.ml
$ ./a.out
$ echo $?
0
]}

And now we have proof that our 2 processes ran "at the same time". We say that
they ran cooperatively. Sleepers are a good example for understanding the
syscalls mechanism with [miou], but of course you can extend this yourself with
[read], [write] and [select] as functions notifying us of system events.

{2 The reason behind this API.}

The notion of "injection" was mentioned a few times, which consists of giving
the user the possibility of implementing the syscalls they want. The main reason
for this API is that it is not dependent on the [Unix] module. In fact, apart
from being particularly consistent, we believe that the I/O issue is not an
issue that fits into the {i scheduling} problem, but is intrinsic to scheduling.
In other words, we can make a library about {i scheduling} that does not require
and provide I/O solutions.

What's more, the I/O is actually quite subtle from one system to another. The
behaviour of [connect()], for example, bears witness to this between {b Linux}
and {b *BSD}. This means that we have to come up with an implementation that
behaves in the same way regardless of the system - as you can imagine, this is a
tedious and difficult job. Above all, it's work that may have nothing to do with
scheduling.

So the aim is to transfer all this complexity away from what [miou] does best:
scheduling. Of course, we're not leaving the user as it is, and we're offering
an extension to [miou] with the implemented I/O cleverly called [miouu] ([miou]
with [U]nix).

{2 Events & domains.}

As you can imagine, this little introduction is not complete if we take into
account {!val:Miou.call}. [miou] can launch tasks in parallel and these tasks
can perform I/O. In our example, we can replace {!val:Miou.call_cc} with
{!val:Miou.call}. The problems that will arise from such a change will be, to
say the least, difficult to explain in full. However, they focus on a point that
is fairly simple to see: we are {b not} protecting our [sleepers] from changes
that several domains can make {i at the same time}.

Overall, this often requires {i synchronisation mechanisms} between domains in
order to manage parallel access to our [sleepers]. However, if you have already
done some parallel programming, these mechanisms can:
- be cumbersome and require resources such as [Mutex], [Condition], etc.
- be error prone in very subtle cases of how domains will react.

Based on these findings, we propose a fairly simple design: a {i syscall}
is {b always} managed by the domain that launched it (it is somewhat equivalent
to {!val:Miou.call_cc}).

{3 Local events at domains and local storage.}

So, if we consider promises that can suspend the flow of execution that are
always local to a domain, we can consider that each domain should have its own
[sleepers] and that access to them should only be made by a single domain (the
one with which they are associated).

From this idea, you can use a {i local storage}. OCaml proposes that you can
associate values with domains and retrieve these values according to the domain.
This is done using the {!module:Domain.DLS} module.

{[
let sleepers =
  let make () = Hashtbl.create 0x100 in
  let key = Domain.DLS.new_key make in
  fun () -> Domain.DLS.get key
]}

We then just need to call [sleepers ()] in all the places where we use our
hash-table to make sure we're using the one that's local to the domain. And
voilÃ ! As you can see, using {i Domain Local Storage} simplifies our code
enormously and saves us from having to implement and manage synchronisation
mechanisms between domains.

{3 Cancellation & interruption.}

There is, however, one final point that we have deliberately omitted from this
little tutorial: interruption. It was explained above that our [events] function
can block and that it's no big deal - in fact, it is. We need to rephrase this
assumption: [events] can block, but there must be a way for [miou] to unblock
the function - and by extension, the domain.

It's fair to ask why we would need such a mechanism. The answer is cancellation.
It is possible to {!val:Miou.cancel} a task with [miou].

{[
let prgm () =
  Miou.run ~events @@ fun () ->
  let a = Miou.call (fun () -> sleep 10.) in
  sleep 1.; Miou.cancel a;
  match Miou.await a with
  | Error Miou.Cancelled -> ()
  | _ -> failwith "test"

let () =
  let t0 = Unix.gettimeofday () in
  prgm () ;
  let t1 = Unix.gettimeofday ()  in
  assert (t1 -. t0 < 10.)
]}

In this example, a domain is asked to [sleep] for 10 seconds. But, at the same
time, we want to {!val:Miou.cancel} this task. At the moment, the domain will
wait 10 seconds and then be "cancelled". This is where the interrupt mechanism
comes in: [miou] will interrupt the domain to tell it that something in its
tasks has changed (cancellation). The domain will then recalculate these tasks
and re-observe their states before finally realising that the task it was doing
has just been cancelled.

The problem is that this interrupt must also interrupt our [Unix.sleepf] on
which our domain is based. It's here, in our [events] function, that we're going
to replace [Unix.sleepf] (which can't be interrupted) with [Unix.select]!

In fact, [Unix.select] can both wait (like [Unix.sleepf]) and interrupt itself
if an event occurs on one of its file-descriptors. We are going to use the
latter mechanism to implement an {i interrupt} mechanism. To do this, we need to
create a pipe ([Unix.pipe]). The [interrupt] function will be called by [miou]
whenever domains need to be interrupted (as in the case of cancellation). This
interruption consists of writing to one side of the pipe while [Unix.select]
observes the other side.

We also need to handle only promises that are pending. A cancellation changes
the state of our promise and we need to "clean up" the promises that have been
cancelled by [miou] in our sleepers.

Finally, we will have to manage 2 cases, the one where we receive an interrupt
and the one where we have just consumed the minimum amount of time between all
our sleepers. In the first case, we'll need to consume the byte sent to us by
[miou], while the second case is similar to what we did before.

{[
let rec consume_interrupt ic =
  if Unix.read ic (Bytes.create 1) 0 1 = 0 then consume_interrupt ic

let update sleepers n =
  Hashtbl.filter_map_inplace
    (fun _ (prm, until) ->
      let until' = Float.max 0. (until -. n) in
      Some (prm, until'))
    sleepers

let select interrupt () =
  let sleepers = sleepers () in
  (* clean-up our sleepers. *)
  Hashtbl.filter_map_inplace
    (fun _ (prm, until) ->
      if Miou.is_pending prm then Some (prm, until) else None)
    sleepers;
  let min =
    Hashtbl.fold
      (fun uid (prm, until) -> function
        | Some (_uid', _prm', until') when until < until' ->
            Some (uid, prm, until)
        | Some _ as acc -> acc
        | None -> Some (uid, prm, until))
      sleepers None
  in
  let ts =
    Option.map (fun (_, _, until) -> until) min |> function
    | Some ts -> ts
    | None -> 0. (* don't wait *) in
  let t0 = Unix.gettimeofday ()
  (* we must record how long we [select ()] to update then our [sleepers]. *)
  match Unix.select [ interrupt ] [] [] ts with
  | [], _, _ -> (
    (* no interruption *)
    let t1 = Unix.gettimeofday () in
    update sleepers (t1 -. t0);
    match min with
    | Some (uid, prm, _) ->
      Hashtbl.remove sleepers uid;
      [ Miou.task prm (Fun.const ()) ]
    | None -> [])
  | _ ->
    (* we got an interruption signal *)
    let t1 = Unix.gettimeofday () in
    consume_interrupt interrupt;
    update sleepers (t1 -. t0);
    []

let events _ =
  let ic, oc = Unix.pipe ~cloexec:true () in
  let rec interrupt () =
    if Unix.write oc (Bytes.make 1 '\000') 0 1 = 0 then interrupt () in
  { Miou.select= select ic; interrupt }
]}

And there you have it, if you run our example code with cancellation, you can
see the interrupt mechanism and the fact that one of our promises is no longer
waiting to be resolved (since it has been cancelled by [miou]). And our program
finishes after 1 second.

This code shows the basic architecture of a real scheduler. We centralise
everything around the [select] (which can become [poll] or something else).
Quite a few issues have not been mentioned here (such as signal management,
system interruption, or how to properly close our pipes). Above all, this means
that this code is just an example! It does, however, give a general idea of how
[miouu] ([miou]'s Unix extension) works and how you can extend [miou] for more
specific applications.

{2 Conclusion.}

The objective of such composability lies in our experience with the use of
several systems, up to the most exotic (such as unikernels), where the ambition
to centralise all the subtleties of these systems in a 'portable' implementation
ultimately remains a vain ambition.

We can, and do, acknowledge the differences between systems, but also
acknowledge the differences in the way in which the events of these systems are
managed. Indeed, there may be alternatives (sometimes more effective) for
managing events that may be in opposition to a more traditional vision (such as
the use of [select]).

Finally, such an ambition requires quite a lot of work, which we can't commit to
in the (very) long term given the resources we have. We prefer to leave room for
future contributors and users who will surely have more time and more skills in
this area.

At the very least, we hope that this little tutorial has taught you a little
more about OCaml, Miou and schedulers. Who knows, you might be able to make your
own!
